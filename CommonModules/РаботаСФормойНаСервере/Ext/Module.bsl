
Процедура ОчиститьФорму(ЭтотОбъект) Экспорт
	КолонкиДляУдаления = Новый Массив;
	
	Пока ЭтотОбъект.Элементы.ДанныеОбъекта.ПодчиненныеЭлементы.Количество() <> 0 Цикл
		ПодчиненныйЭлементСтраница = ЭтотОбъект.Элементы.ДанныеОбъекта.ПодчиненныеЭлементы[0];
		
		Для Каждого ПодчиненныйЭлементКолонка Из ПодчиненныйЭлементСтраница.ПодчиненныеЭлементы Цикл
			КолонкиДляУдаления.Добавить(ПодчиненныйЭлементКолонка.ПутьКДанным);
		КонецЦикла;
		
		ЭтотОбъект.Элементы.Удалить(ПодчиненныйЭлементСтраница);
	КонецЦикла;
	
	Пока ЭтотОбъект.Элементы.СоздатьНаОсновании.ПодчиненныеЭлементы.Количество() <> 0 Цикл
		ПодчиненныйЭлементКнопка = ЭтотОбъект.Элементы.СоздатьНаОсновании.ПодчиненныеЭлементы[0];
		
		ЭтотОбъект.Элементы.Удалить(ПодчиненныйЭлементКнопка);
	КонецЦикла;
	
	ЭтотОбъект.ИзменитьРеквизиты(, КолонкиДляУдаления);
	
	Если ТипЗнч(ЭтотОбъект.КомандыКнопок) = Тип("ФиксированнаяСтруктура") Тогда
		Для Каждого КомандаКнпоки Из ЭтотОбъект.КомандыКнопок Цикл
			ЭтотОбъект.Команды.Удалить(ЭтотОбъект.Команды.Найти(КомандаКнпоки.Ключ));
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьФорму(Форма) Экспорт
	ПериодДанных = Неопределено;
	ИсточникДанных = Неопределено;
	
	Если ЗначениеЗаполнено(Форма.Копия) Тогда
		ИсточникДанных = Форма.Копия;
	ИначеЕсли (Форма.Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Форма.Параметры.ЗначениеКопирования)) Тогда
		Форма.Копия = Форма.Параметры.ЗначениеКопирования;
		ИсточникДанных = Форма.Параметры.ЗначениеКопирования;
	Иначе
		Если Форма.Параметры.Свойство("ЗначенияЗаполнения")
			И Форма.Параметры.ЗначенияЗаполнения.Свойство("Команда") Тогда
			Если Форма.Параметры.ЗначенияЗаполнения.Команда = "СоздатьНаОсновании" Тогда
				ИсточникДанных = Форма.Объект.Основание;
			ИначеЕсли Форма.Параметры.ЗначенияЗаполнения.Команда = "СоздатьКорректировку" Тогда
				ИсточникДанных = Форма.Объект.Корректировка;
			ИначеЕсли Форма.Параметры.ЗначенияЗаполнения.Команда = "СоздатьВерсию" Тогда
				ИсточникДанных = Форма.Объект.Версия;
			КонецЕсли;
		Иначе
			Если Форма.Объект.Корректировка.Пустая() Тогда
				ИсточникДанных = Форма.Объект.Ссылка;
			Иначе
				ИсточникДанных = Форма.Объект.Корректировка;
			КонецЕсли;
			
			Если Не ИсточникДанных.Пустая()
				И РаботаСОбъектомНаСервере.ВестиКорректировки(Форма.Объект.ОбъектСистемы, Форма.Объект.ВариантОбъекта) Тогда
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("Владелец", ИсточникДанных);
				Запрос.УстановитьПараметр("Источник", Форма.Объект.Ссылка);
				Запрос.Текст = ПолучитьЗапросПериодаДанных();
				Результат = Запрос.Выполнить();
				
				Если Результат.Пустой() Тогда
					ПериодДанных = Форма.Объект.Дата;
				Иначе
					Результат = Результат.Выбрать();
					Результат.Следующий();
					
					ПериодДанных = Результат.Период;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПериодДанных", ПериодДанных);
	Запрос.УстановитьПараметр("ИсточникДанных", ИсточникДанных);
	Запрос.УстановитьПараметр("ОбъектСистемыИсточникаДанных", ?(ИсточникДанных = Неопределено, Форма.Объект.ОбъектСистемы, ИсточникДанных.ОбъектСистемы));
	Запрос.УстановитьПараметр("ОбъектСистемы", Форма.Объект.ОбъектСистемы);
	Запрос.УстановитьПараметр("ВариантОбъекта", Форма.Объект.ВариантОбъекта);
	Запрос.Текст = ПолучитьЗапросДанныеОбъектаСистемы();
	Результат = Запрос.ВыполнитьПакет();
	
	КолонкиФормы = Результат[8];
	ДанныеФормы = Результат[9];
	
	Таблица = КолонкиФормы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ДобавляемыеКолонки = Новый Массив;
	
	Пока Таблица.Следующий() Цикл
		Путь = "";
		
		Если Таблица.Таблица <> Форма.Объект.ОбъектСистемы Тогда
			Путь = Таблица.Таблица.СистемноеНаименование;
			
			НовыйКолонка = Новый РеквизитФормы(Путь, Новый ОписаниеТипов("ТаблицаЗначений"));
			ДобавляемыеКолонки.Добавить(НовыйКолонка);
			
			НовыйКолонка = Новый РеквизитФормы("СтрокаТаблицы", Новый ОписаниеТипов("СправочникСсылка.СтрокиТаблиц"), Путь, "Строка таблицы");
			ДобавляемыеКолонки.Добавить(НовыйКолонка);
		КонецЕсли;
		
		Колонка = Таблица.Выбрать();
		
		Пока Колонка.Следующий() Цикл
			НовыйКолонка = Новый РеквизитФормы(Колонка.Колонка.СистемноеНаименование, Колонка.Колонка.ТипЗначения, Путь, СтрЗаменить(Колонка.Колонка.Наименование, " _", ""));
			ДобавляемыеКолонки.Добавить(НовыйКолонка);
		КонецЦикла;
	КонецЦикла;
	
	Форма.ИзменитьРеквизиты(ДобавляемыеКолонки);
	
	Таблица.Сбросить();
	
	Пока Таблица.Следующий() Цикл
		СтраницаДляДобавления = Форма.Элементы.Добавить(Таблица.Таблица.СистемноеНаименование, Тип("ГруппаФормы"), Форма.Элементы.ДанныеОбъекта);
		СтраницаДляДобавления.Вид = ВидГруппыФормы.Страница;
		
		//СтраницаДляДобавления.ТолькоПросмотр = Не Таблица.СохранятьТаблицу Или Не Таблица.ВводТаблицы;
		СтраницаДляДобавления.ШрифтЗаголовка = Новый Шрифт(СтраницаДляДобавления.ШрифтЗаголовка, , , Не Таблица.ВводТаблицы, , Не Таблица.СохранятьТаблицу);
		
		РодительЭлемента = СтраницаДляДобавления;
		ПутьКДаннымТаблицы = "";
		
		Если Таблица.Таблица = Форма.Объект.ОбъектСистемы Тогда
			СтраницаДляДобавления.Заголовок = "Реквизиты объекта";
		Иначе
			СтраницаДляДобавления.Заголовок = Таблица.Таблица.Наименование;
			
			ТаблицаДокумента = Форма.Элементы.Добавить(СтрШаблон("Таблица%1", Таблица.Таблица.СистемноеНаименование), Тип("ТаблицаФормы"), РодительЭлемента);
			ТаблицаДокумента.ПутьКДанным = Таблица.Таблица.СистемноеНаименование;
			
			РодительЭлемента = ТаблицаДокумента;
			ПутьКДаннымТаблицы = СтрШаблон("%1.", Таблица.Таблица.СистемноеНаименование);
		КонецЕсли;
		
		Колонка = Таблица.Выбрать();
		
		Пока Колонка.Следующий() Цикл
			НовыйЭлемент = Форма.Элементы.Добавить(СтрШаблон("%1%2", Таблица.Таблица.СистемноеНаименование, Колонка.Колонка.СистемноеНаименование), Тип("ПолеФормы"), РодительЭлемента);
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.ПутьКДанным = СтрШаблон("%1%2", ПутьКДаннымТаблицы, Колонка.Колонка.СистемноеНаименование);
			НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;
			
			Если Колонка.Колонка.ТипЗначения.СодержитТип(Тип("Строка"))
				И Колонка.Колонка.ТипЗначения.КвалификаторыСтроки.Длина = 0 Тогда
				НовыйЭлемент.АвтоМаксимальнаяШирина = Ложь;
				НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
			КонецЕсли;
			
			//НовыйЭлемент.ТолькоПросмотр = Не Таблица.СохранятьТаблицу Или Не Таблица.ВводТаблицы Или Не Колонка.СохранятьКолонку Или Не Колонка.ВводКолонки;
			НовыйЭлемент.ШрифтЗаголовка = Новый Шрифт(НовыйЭлемент.ШрифтЗаголовка, , , Не Таблица.ВводТаблицы Или Не Колонка.ВводКолонки, , Не Таблица.СохранятьТаблицу Или Не Колонка.СохранятьКолонку);
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Колонка", Колонка.Колонка);
			Запрос.Текст = ПолучитьЗапросПараметрыВыбора();
			Результат = Запрос.Выполнить().Выгрузить();
			
			НовыйМассив = Новый Массив();
			НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ОбъектСистемы", Результат.ВыгрузитьКолонку("ОбъектСистемы")));
			НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ВариантОбъекта", Результат.ВыгрузитьКолонку("ВариантОбъекта")));
			НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Корректировка", Документы.ЭлементыОбъектовСистемы.ПустаяСсылка()));
			
			Если Не Колонка.Колонка.ПоказыватьВерсии Тогда
				НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Версия", Документы.ЭлементыОбъектовСистемы.ПустаяСсылка()));
			КонецЕсли;
			
			НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
			НовыйЭлемент.ПараметрыВыбора = НовыеПараметры;
			
			Если Колонка.Колонка = ПланыВидовХарактеристик.КолонкиОбъектов.ВидДвижения Тогда
				НовыйЭлемент.Формат = "БЛ=Расход; БИ=Приход";
				НовыйЭлемент.ФорматРедактирования = "БЛ=Расход; БИ=Приход";
			КонецЕсли;
			
			Если ПутьКДаннымТаблицы <> "" Тогда
				НовыйЭлемент.Ширина = 1;
			КонецЕсли;
			
			//Проверка = "";
			////Проверка = "То_";
			////Проверка = "Элементы.ТаблицаТаблицаПерсонал.ТекущиеДанные.То_";
			//Если Проверка = "" Тогда
			//	НовыйМассив = Новый Массив();
			//	НовыйМассив.Добавить(Новый СвязьПараметраВыбора("Отбор.Основание", Проверка));
			//	НовыйЭлемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(НовыйМассив);	
			//КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Таблица = ДанныеФормы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Пока Таблица.Следующий() Цикл
		//Если Не Таблица.СохранятьТаблицу Тогда
		//	Продолжить;
		//КонецЕсли;
		
		СтрокаТаблицы = Таблица.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		Если ЗначениеЗаполнено(Таблица.СтрокаТаблицы) Тогда
			Если Таблица.Таблица = Форма.Объект.ОбъектСистемы Тогда
				Пока СтрокаТаблицы.Следующий() Цикл
					Колонка = СтрокаТаблицы.Выбрать();
					
					Пока Колонка.Следующий() Цикл
						//Если Не Колонка.СохранятьКолонку Тогда
						//	Продолжить;
						//КонецЕсли;
						
						Форма[Колонка.Колонка.СистемноеНаименование] = Колонка.Значение;
					КонецЦикла;
				КонецЦикла;
			Иначе
				Пока СтрокаТаблицы.Следующий() Цикл
					НоваяСтрока = Форма[Таблица.Таблица.СистемноеНаименование].Добавить();
					НоваяСтрока.СтрокаТаблицы = СтрокаТаблицы.СтрокаТаблицы;
					
					Колонка = СтрокаТаблицы.Выбрать();
					
					Пока Колонка.Следующий() Цикл
						Если Колонка.СохранятьКолонку Тогда
							НоваяСтрока[Колонка.Колонка.СистемноеНаименование] = Колонка.Значение;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура НастроитьФорму(ЭтотОбъект, Объект) Экспорт
	// кнопки создания на основании
	
	КомандыКнопок = Новый Структура;
	
	Если Объект.ОбъектСистемы.ВестиВерсии 
		И (Не Объект.ОбъектСистемы.ВестиВариантыОбъектов
		Или Объект.ВариантОбъекта.ВестиВерсии) Тогда
		ИмяКоманды = "СоздатьВерсию";
		ЗаголовокКнопки = "Создать версию";
		
		НоваяКоманда = ЭтотОбъект.Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Действие = "СоздатьНаОсновании";
		
		НоваяКнопка = ЭтотОбъект.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭтотОбъект.Элементы.СоздатьНаОсновании);
		НоваяКнопка.УникальностьКоманды = Ложь;
		НоваяКнопка.Заголовок = ЗаголовокКнопки;
		НоваяКнопка.ИмяКоманды = ИмяКоманды;
		
		КомандыКнопок.Вставить(ИмяКоманды, ИмяКоманды);
	КонецЕсли;
	
	Если Объект.ОбъектСистемы.ВестиКорректировки 
		И (Не Объект.ОбъектСистемы.ВестиВариантыОбъектов
		Или Объект.ВариантОбъекта.ВестиКорректировки) Тогда
		ИмяКоманды = "СоздатьКорректировку";
		ЗаголовокКнопки = "Создать корректировку";
		
		НоваяКоманда = ЭтотОбъект.Команды.Добавить(ИмяКоманды);
		НоваяКоманда.Действие = "СоздатьНаОсновании";
		
		НоваяКнопка = ЭтотОбъект.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭтотОбъект.Элементы.СоздатьНаОсновании);
		НоваяКнопка.УникальностьКоманды = Ложь;
		НоваяКнопка.Заголовок = ЗаголовокКнопки;
		НоваяКнопка.ИмяКоманды = ИмяКоманды;
		
		КомандыКнопок.Вставить(ИмяКоманды, ИмяКоманды);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектСистемы", Объект.ОбъектСистемы);
	Запрос.УстановитьПараметр("ВариантОбъекта", Объект.ВариантОбъекта);
	Запрос.Текст = ПолучитьЗапросОснованияОбъектаСистемы();
	РезультатОбъектыСистемы = Запрос.Выполнить();
	
	Если Не РезультатОбъектыСистемы.Пустой() Тогда
		РезультатОбъектыСистемы = РезультатОбъектыСистемы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		Пока РезультатОбъектыСистемы.Следующий() Цикл
			Если РезультатОбъектыСистемы.ВариантОбъекта = Справочники.ВариантыОбъектов.ПустаяСсылка() Тогда
				ИмяКоманды = СтрШаблон("%1%2%3", РезультатОбъектыСистемы.ОбъектСистемы.СистемноеНаименование, "000000001", "Основание");
				ЗаголовокКнопки = РезультатОбъектыСистемы.ОбъектСистемы.Наименование + " (Основание)";
				
				НоваяКоманда = ЭтотОбъект.Команды.Добавить(ИмяКоманды);
				НоваяКоманда.Действие = "СоздатьНаОсновании";
				
				НоваяКнопка = ЭтотОбъект.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭтотОбъект.Элементы.СоздатьНаОсновании);
				НоваяКнопка.УникальностьКоманды = Ложь;
				НоваяКнопка.Заголовок = ЗаголовокКнопки;
				НоваяКнопка.ИмяКоманды = ИмяКоманды;
				
				КомандыКнопок.Вставить(ИмяКоманды, Новый Структура("ОбъектСистемы, ВариантОбъекта", РезультатОбъектыСистемы.ОбъектСистемы, Справочники.ВариантыОбъектов.ПустаяСсылка()));
			Иначе
				Родитель = ЭтотОбъект.Элементы.Добавить(СтрШаблон("%1%2", "ГруппаФормы", РезультатОбъектыСистемы.ОбъектСистемы.СистемноеНаименование), Тип("ГруппаФормы"), ЭтотОбъект.Элементы.СоздатьНаОсновании);
				Родитель.Вид = ВидГруппыФормы.Подменю;
				Родитель.Заголовок = РезультатОбъектыСистемы.ОбъектСистемы.Наименование;
				
				РезультатВариантыОбъектов = РезультатОбъектыСистемы.Выбрать();
				
				Пока РезультатВариантыОбъектов.Следующий() Цикл
					ИмяКоманды = СтрШаблон("%1%2%3", РезультатОбъектыСистемы.ОбъектСистемы.СистемноеНаименование, РезультатВариантыОбъектов.ВариантОбъекта.Код, "Основание");
					ЗаголовокКнопки = РезультатВариантыОбъектов.ВариантОбъекта.Наименование + " (Основание)";
					
					НоваяКоманда = ЭтотОбъект.Команды.Добавить(ИмяКоманды);
					НоваяКоманда.Действие = "СоздатьНаОсновании";
					
					НоваяКнопка = ЭтотОбъект.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Родитель);
					НоваяКнопка.УникальностьКоманды = Ложь;
					НоваяКнопка.Заголовок = ЗаголовокКнопки;
					НоваяКнопка.ИмяКоманды = ИмяКоманды;
					
					КомандыКнопок.Вставить(ИмяКоманды, Новый Структура("ОбъектСистемы, ВариантОбъекта", РезультатОбъектыСистемы.ОбъектСистемы, РезультатВариантыОбъектов.ВариантОбъекта));
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст = ПолучитьЗапросИсточникиОбъектаСистемы();
	РезультатОбъектыСистемы = Запрос.Выполнить();
	
	Если Не РезультатОбъектыСистемы.Пустой() Тогда
		РезультатОбъектыСистемы = РезультатОбъектыСистемы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		Пока РезультатОбъектыСистемы.Следующий() Цикл
			Если РезультатОбъектыСистемы.ВариантОбъекта = Справочники.ВариантыОбъектов.ПустаяСсылка() Тогда
				ИмяКоманды = СтрШаблон("%1%2%3", РезультатОбъектыСистемы.ОбъектСистемы.СистемноеНаименование, "000000001", "Источник");
				ЗаголовокКнопки = РезультатОбъектыСистемы.ОбъектСистемы.Наименование + " (Источник)";
				
				НоваяКоманда = ЭтотОбъект.Команды.Добавить(ИмяКоманды);
				НоваяКоманда.Действие = "СоздатьНаОсновании";
				
				НоваяКнопка = ЭтотОбъект.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭтотОбъект.Элементы.СоздатьНаОсновании);
				НоваяКнопка.УникальностьКоманды = Ложь;
				НоваяКнопка.Заголовок = ЗаголовокКнопки;
				НоваяКнопка.ИмяКоманды = ИмяКоманды;
				
				КомандыКнопок.Вставить(ИмяКоманды, Новый Структура("ОбъектСистемы, ВариантОбъекта", РезультатОбъектыСистемы.ОбъектСистемы, Справочники.ВариантыОбъектов.ПустаяСсылка()));
			Иначе
				Родитель = ЭтотОбъект.Элементы.Добавить(СтрШаблон("%1%2", "ГруппаФормы", РезультатОбъектыСистемы.ОбъектСистемы.СистемноеНаименование), Тип("ГруппаФормы"), ЭтотОбъект.Элементы.СоздатьНаОсновании);
				Родитель.Вид = ВидГруппыФормы.Подменю;
				Родитель.Заголовок = РезультатОбъектыСистемы.ОбъектСистемы.Наименование;
				
				РезультатВариантыОбъектов = РезультатОбъектыСистемы.Выбрать();
				
				Пока РезультатВариантыОбъектов.Следующий() Цикл
					ИмяКоманды = СтрШаблон("%1%2%3", РезультатОбъектыСистемы.ОбъектСистемы.СистемноеНаименование, РезультатВариантыОбъектов.ВариантОбъекта.Код, "Источник");
					ЗаголовокКнопки = РезультатВариантыОбъектов.ВариантОбъекта.Наименование + " (Источник)";
					
					НоваяКоманда = ЭтотОбъект.Команды.Добавить(ИмяКоманды);
					НоваяКоманда.Действие = "СоздатьНаОсновании";
					
					НоваяКнопка = ЭтотОбъект.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Родитель);
					НоваяКнопка.УникальностьКоманды = Ложь;
					НоваяКнопка.Заголовок = ЗаголовокКнопки;
					НоваяКнопка.ИмяКоманды = ИмяКоманды;
					
					КомандыКнопок.Вставить(ИмяКоманды, Новый Структура("ОбъектСистемы, ВариантОбъекта", РезультатОбъектыСистемы.ОбъектСистемы, РезультатВариантыОбъектов.ВариантОбъекта));
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЭтотОбъект.КомандыКнопок = Новый ФиксированнаяСтруктура(КомандыКнопок);
	
	// настройка элементов
	
	ЭтотОбъект.Элементы.Копия.Видимость = Объект.Ссылка.Пустая() И Не ЭтотОбъект.Копия.Пустая();
	
	ЭтотОбъект.Элементы.ВариантОбъекта.Видимость = Объект.ОбъектСистемы.ВестиВариантыОбъектов;
	ЭтотОбъект.Элементы.Корректировка.Видимость = Не Объект.Корректировка.Пустая();
	ЭтотОбъект.Элементы.Версия.Видимость = Не Объект.Версия.Пустая();
	ЭтотОбъект.Элементы.Наименование.Видимость = Объект.ОбъектСистемы.ТипОбъекта = Справочники.ТипыОбъектов.Справочник;
	ЭтотОбъект.Элементы.Номер.Заголовок = ?(Объект.ОбъектСистемы.ТипОбъекта = Справочники.ТипыОбъектов.Справочник, "Код", "Номер");
	
	// нужно бы перепроверить... может быть заблокировать изменение объекта системы с вариант варианты объектов и убрать код ниже
	
	Если Объект.Основание.Пустая() Тогда
		ЭтотОбъект.Элементы.Основание.Видимость = Ложь;
	Иначе
		ЭтотОбъект.Элементы.Основание.Видимость = Истина;
		
		Запрос.УстановитьПараметр("ОбъектСистемы", Объект.Основание.ОбъектСистемы);
		Запрос.УстановитьПараметр("ВариантОбъекта", Объект.Основание.ВариантОбъекта);
		Результат = Запрос.Выполнить().Выгрузить();
		
		НовыйМассив = Новый Массив();
		НовыеЗначения = Новый ФиксированныйМассив(Результат.ВыгрузитьКолонку("ОбъектСистемы"));
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Ссылка", НовыеЗначения);
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		ЭтотОбъект.Элементы.ОбъектСистемы.ПараметрыВыбора = НовыеПараметры;
		
		НовыйМассив = Новый Массив();
		НовыеЗначения = Новый ФиксированныйМассив(Результат.ВыгрузитьКолонку("ВариантОбъекта"));
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Ссылка", НовыеЗначения);
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		ЭтотОбъект.Элементы.ВариантОбъекта.ПараметрыВыбора = НовыеПараметры;
	КонецЕсли;
	
	ЭтотОбъект.Элементы.Источник.Видимость = Не Объект.Источник.Пустая();
КонецПроцедуры



Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи, ЭтаФорма) Экспорт
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ИнтерактивноеДействие", Истина);
	
	ТаблицаКолонок = РегистрыСведений.ХранилищеЗначенийКолонокОбъектов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	СохранениеКолонок = Новый Соответствие;
	
	ЗаполнитьНаименование = ПустаяСтрока(ТекущийОбъект.Наименование) И ТекущийОбъект.ОбъектСистемы.ТипОбъекта = Справочники.ТипыОбъектов.Справочник;
	
	Для Каждого СтрокаКолонка Из ТекущийОбъект.ОбъектСистемы.Колонки Цикл
		Если СтрокаКолонка.Исключить
			Или (Не СтрокаКолонка.ВариантОбъекта.Пустая()
			И СтрокаКолонка.ВариантОбъекта <> ТекущийОбъект.ВариантОбъекта) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяКолонки = СтрокаКолонка.Колонка.СистемноеНаименование;
		ИмяКолонкиНаФорме = ТекущийОбъект.ОбъектСистемы.СистемноеНаименование + ИмяКолонки; // нужна универсальная функция потому что при создании формы делается тоже самое и нужно чтобы это было одинаково (если вдруг там поменяют)
		
		Если ЭтаФорма.Элементы.Найти(ИмяКолонкиНаФорме) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаКолонок.Добавить();
		НоваяСтрока.ОбъектСистемы = ТекущийОбъект.ОбъектСистемы;
		НоваяСтрока.Колонка = СтрокаКолонка.Колонка;
		НоваяСтрока.Значение = ЭтаФорма[ИмяКолонки];
		
		СохранениеКолонок.Вставить(СтрокаКолонка.Колонка, СтрокаКолонка.Сохранять);
		
		Если ЗаполнитьНаименование Тогда
			Если СтрокаКолонка.ВключитьВНаименование Тогда
				ТекущийОбъект.Наименование = СтрШаблон("%1%2", ТекущийОбъект.Наименование, ?(ТекущийОбъект.Наименование = "", Строка(НоваяСтрока.Значение), СтрШаблон(" / %1",  Строка(НоваяСтрока.Значение))));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ТаблицаКолонок", ТаблицаКолонок);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("СохранениеКолонок", СохранениеКолонок);
	
	ТаблицыОбъекта = Новый Соответствие;
	СохранениеТаблиц = Новый Соответствие;
	
	Для Каждого СтрокаТаблицы Из ТекущийОбъект.ОбъектСистемы.Таблицы Цикл
		Если СтрокаТаблицы.Исключить
			Или (Не СтрокаТаблицы.ВариантОбъекта.Пустая()
			И СтрокаТаблицы.ВариантОбъекта <> ТекущийОбъект.ВариантОбъекта) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяТаблицы = СтрокаТаблицы.Таблица.СистемноеНаименование;
		
		Если ЭтаФорма.Элементы.Найти(ИмяТаблицы) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаОбъекта = ЭтаФорма[ИмяТаблицы].Выгрузить();
		ТаблицыОбъекта.Вставить(СтрокаТаблицы.Таблица, ТаблицаОбъекта);
		
		СохранениеТаблиц.Вставить(СтрокаТаблицы.Таблица, СтрокаТаблицы.Сохранять);
	КонецЦикла;
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ТаблицыОбъекта", ТаблицыОбъекта);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("СохранениеТаблиц", СохранениеТаблиц);
КонецПроцедуры



#Область Запросы

Функция ПолучитьЗапросДанныеОбъектаСистемы()
	Возврат
	
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОбъектыСистемыТаблицы.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	ОбъектыСистемыТаблицы.Таблица КАК Таблица,
	|	ОбъектыСистемыТаблицы.Исключить КАК Исключить,
	|	ОбъектыСистемыТаблицы.Ввод КАК ВводТаблицы,
	|	ОбъектыСистемыТаблицы.Сохранять КАК СохранятьТаблицу
	|ПОМЕСТИТЬ ВТ_ТаблицыТемп
	|ИЗ
	|	Справочник.ОбъектыСистемы.Таблицы КАК ОбъектыСистемыТаблицы
	|ГДЕ
	|	ОбъектыСистемыТаблицы.Ссылка = &ОбъектСистемы
	|	И (ОбъектыСистемыТаблицы.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|			ИЛИ ОбъектыСистемыТаблицы.ВариантОбъекта = &ВариантОбъекта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицыТемп.Таблица КАК Таблица,
	|	МАКСИМУМ(ВТ_ТаблицыТемп.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ВТ_ТаблицыМакс
	|ИЗ
	|	ВТ_ТаблицыТемп КАК ВТ_ТаблицыТемп
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицыТемп.Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицыТемп.Таблица КАК Таблица,
	|	ВТ_ТаблицыТемп.ВводТаблицы КАК ВводТаблицы,
	|	ВТ_ТаблицыТемп.СохранятьТаблицу КАК СохранятьТаблицу
	|ПОМЕСТИТЬ ВТ_Таблицы
	|ИЗ
	|	ВТ_ТаблицыМакс КАК ВТ_ТаблицыМакс
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицыТемп КАК ВТ_ТаблицыТемп
	|		ПО ВТ_ТаблицыМакс.Приоритет = ВТ_ТаблицыТемп.Приоритет
	|			И ВТ_ТаблицыМакс.Таблица = ВТ_ТаблицыТемп.Таблица
	|ГДЕ
	|	НЕ ВТ_ТаблицыТемп.Исключить
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОбъектыСистемыКолонки.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	ОбъектыСистемыКолонки.Колонка КАК Колонка,
	|	ОбъектыСистемыКолонки.Исключить КАК Исключить,
	|	ОбъектыСистемыКолонки.Ввод КАК ВводКолонки,
	|	ОбъектыСистемыКолонки.Сохранять КАК СохранятьКолонку
	|ПОМЕСТИТЬ ВТ_КолонкиТемп
	|ИЗ
	|	Справочник.ОбъектыСистемы.Колонки КАК ОбъектыСистемыКолонки
	|ГДЕ
	|	ОбъектыСистемыКолонки.Ссылка = &ОбъектСистемы
	|	И (ОбъектыСистемыКолонки.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|			ИЛИ ОбъектыСистемыКолонки.ВариантОбъекта = &ВариантОбъекта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КолонкиТемп.Колонка КАК Колонка,
	|	МАКСИМУМ(ВТ_КолонкиТемп.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ВТ_КолонкиМакс
	|ИЗ
	|	ВТ_КолонкиТемп КАК ВТ_КолонкиТемп
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_КолонкиТемп.Колонка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ОбъектСистемы КАК Таблица,
	|	&ОбъектСистемыИсточникаДанных КАК ТаблицаИсточникаДанных,
	|	ИСТИНА КАК ВводТаблицы,
	|	ИСТИНА КАК СохранятьТаблицу,
	|	ВТ_КолонкиТемп.Колонка КАК Колонка,
	|	ВТ_КолонкиТемп.ВводКолонки КАК ВводКолонки,
	|	ВТ_КолонкиТемп.СохранятьКолонку КАК СохранятьКолонку
	|ПОМЕСТИТЬ ВТ_КолонкиТаблиц
	|ИЗ
	|	ВТ_КолонкиМакс КАК ВТ_КолонкиМакс
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КолонкиТемп КАК ВТ_КолонкиТемп
	|		ПО ВТ_КолонкиМакс.Приоритет = ВТ_КолонкиТемп.Приоритет
	|			И ВТ_КолонкиМакс.Колонка = ВТ_КолонкиТемп.Колонка
	|ГДЕ
	|	НЕ ВТ_КолонкиТемп.Исключить
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Таблицы.Таблица,
	|	ВТ_Таблицы.Таблица,
	|	ВТ_Таблицы.ВводТаблицы,
	|	ВТ_Таблицы.СохранятьТаблицу,
	|	ОбъектыСистемыКолонки.Колонка,
	|	ОбъектыСистемыКолонки.Ввод,
	|	ОбъектыСистемыКолонки.Сохранять
	|ИЗ
	|	ВТ_Таблицы КАК ВТ_Таблицы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСистемы.Колонки КАК ОбъектыСистемыКолонки
	|		ПО ВТ_Таблицы.Таблица = ОбъектыСистемыКолонки.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктивностьСтрокТаблицСрезПоследних.ОбъектСистемы КАК Таблица,
	|	АктивностьСтрокТаблицСрезПоследних.СтрокаТаблицы КАК СтрокаТаблицы
	|ПОМЕСТИТЬ ВТ_СтрокиТаблиц
	|ИЗ
	|	РегистрСведений.АктивностьСтрокТаблиц.СрезПоследних(&ПериодДанных, Владелец = &ИсточникДанных) КАК АктивностьСтрокТаблицСрезПоследних
	|ГДЕ
	|	АктивностьСтрокТаблицСрезПоследних.АктивностьСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КолонкиТаблиц.Таблица КАК Таблица,
	|	ВТ_КолонкиТаблиц.ТаблицаИсточникаДанных КАК ТаблицаИсточникаДанных,
	|	ВТ_КолонкиТаблиц.ВводТаблицы КАК ВводТаблицы,
	|	ВТ_КолонкиТаблиц.СохранятьТаблицу КАК СохранятьТаблицу,
	|	ВТ_КолонкиТаблиц.Колонка КАК Колонка,
	|	ВТ_КолонкиТаблиц.ВводКолонки КАК ВводКолонки,
	|	ВТ_КолонкиТаблиц.СохранятьКолонку КАК СохранятьКолонку,
	|	ВЫБОР
	|		КОГДА ВТ_КолонкиТаблиц.Таблица = &ОбъектСистемы
	|			ТОГДА &ИсточникДанных
	|		ИНАЧЕ ВТ_СтрокиТаблиц.СтрокаТаблицы
	|	КОНЕЦ КАК СтрокаТаблицы
	|ПОМЕСТИТЬ ВТ_Итог
	|ИЗ
	|	ВТ_КолонкиТаблиц КАК ВТ_КолонкиТаблиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтрокиТаблиц КАК ВТ_СтрокиТаблиц
	|		ПО ВТ_КолонкиТаблиц.Таблица = ВТ_СтрокиТаблиц.Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КолонкиТаблиц.Таблица КАК Таблица,
	|	ВТ_КолонкиТаблиц.ВводТаблицы КАК ВводТаблицы,
	|	ВТ_КолонкиТаблиц.СохранятьТаблицу КАК СохранятьТаблицу,
	|	ВТ_КолонкиТаблиц.Колонка КАК Колонка,
	|	ВТ_КолонкиТаблиц.ВводКолонки КАК ВводКолонки,
	|	ВТ_КолонкиТаблиц.СохранятьКолонку КАК СохранятьКолонку
	|ИЗ
	|	ВТ_КолонкиТаблиц КАК ВТ_КолонкиТаблиц
	|ИТОГИ
	|	МИНИМУМ(ВводТаблицы),
	|	МИНИМУМ(СохранятьТаблицу)
	|ПО
	|	Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Итог.Таблица КАК Таблица,
	|	ВТ_Итог.ВводТаблицы КАК ВводТаблицы,
	|	ВТ_Итог.СохранятьТаблицу КАК СохранятьТаблицу,
	|	ВТ_Итог.Колонка КАК Колонка,
	|	ВТ_Итог.ВводКолонки КАК ВводКолонки,
	|	ВТ_Итог.СохранятьКолонку КАК СохранятьКолонку,
	|	ВТ_Итог.СтрокаТаблицы КАК СтрокаТаблицы,
	|	ХранилищеЗначенийКолонокОбъектовСрезПоследних.Значение КАК Значение
	|ИЗ
	|	ВТ_Итог КАК ВТ_Итог
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХранилищеЗначенийКолонокОбъектов.СрезПоследних(&ПериодДанных, Владелец = &ИсточникДанных) КАК ХранилищеЗначенийКолонокОбъектовСрезПоследних
	|		ПО ВТ_Итог.ТаблицаИсточникаДанных = ХранилищеЗначенийКолонокОбъектовСрезПоследних.ОбъектСистемы
	|			И ВТ_Итог.СтрокаТаблицы = ХранилищеЗначенийКолонокОбъектовСрезПоследних.Объект
	|			И ВТ_Итог.Колонка = ХранилищеЗначенийКолонокОбъектовСрезПоследних.Колонка
	|ИТОГИ
	|	МИНИМУМ(ВводТаблицы),
	|	МИНИМУМ(СохранятьТаблицу),
	|	МАКСИМУМ(СтрокаТаблицы)
	|ПО
	|	Таблица,
	|	СтрокаТаблицы";
КонецФункции

Функция ПолучитьЗапросОснованияОбъектаСистемы()
	Возврат
	
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОбъектыСистемыОснования.ВариантОбъектаОснования = ЗНАЧЕНИЕ(СПРАВОЧНИК.ВариантыОбъектов.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	ОбъектыСистемыОснования.Ссылка КАК ОбъектСистемы,
	|	ОбъектыСистемыОснования.ВариантОбъекта КАК ВариантОбъекта,
	|	ОбъектыСистемыОснования.ИсключитьОснование КАК ИсключитьОснование,
	|	ОбъектыСистемыОснования.ИсключитьВариантОбъекта КАК ИсключитьВариантОбъекта
	|ПОМЕСТИТЬ ВТ_ОснованияТемп
	|ИЗ
	|	Справочник.ОбъектыСистемы.Основания КАК ОбъектыСистемыОснования
	|ГДЕ
	|	ОбъектыСистемыОснования.Основание = &ОбъектСистемы
	//|	И ОбъектыСистемыОснования.Основание.ВестиВариантыОбъектов
	|	И (ОбъектыСистемыОснования.ВариантОбъектаОснования = &ВариантОбъекта
	|			ИЛИ ОбъектыСистемыОснования.ВариантОбъектаОснования = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ОснованияТемп.Приоритет) КАК Приоритет,
	|	ВТ_ОснованияТемп.ОбъектСистемы КАК ОбъектСистемы,
	|	ВТ_ОснованияТемп.ВариантОбъекта КАК ВариантОбъекта
	|ПОМЕСТИТЬ ВТ_ОснованияМакс
	|ИЗ
	|	ВТ_ОснованияТемп КАК ВТ_ОснованияТемп
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОснованияТемп.ОбъектСистемы,
	|	ВТ_ОснованияТемп.ВариантОбъекта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОснованияТемп.ОбъектСистемы КАК ОбъектСистемы,
	|	ВТ_ОснованияТемп.ВариантОбъекта КАК ВариантОбъекта,
	|	ВТ_ОснованияТемп.ИсключитьВариантОбъекта КАК ИсключитьВариантОбъекта
	|ПОМЕСТИТЬ ВТ_Основания
	|ИЗ
	|	ВТ_ОснованияМакс КАК ВТ_ОснованияМакс
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОснованияТемп КАК ВТ_ОснованияТемп
	|		ПО ВТ_ОснованияМакс.Приоритет = ВТ_ОснованияТемп.Приоритет
	|			И ВТ_ОснованияМакс.ОбъектСистемы = ВТ_ОснованияТемп.ОбъектСистемы
	|			И ВТ_ОснованияМакс.ВариантОбъекта = ВТ_ОснованияТемп.ВариантОбъекта
	|ГДЕ
	|	НЕ ВТ_ОснованияТемп.ИсключитьОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ОснованияТемп
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ОснованияМакс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_Основания.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	ВТ_Основания.ОбъектСистемы КАК ОбъектСистемы,
	|	ЕСТЬNULL(ВариантыОбъектов.Ссылка, ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)) КАК ВариантОбъекта,
	|	ВТ_Основания.ИсключитьВариантОбъекта КАК ИсключитьВариантОбъекта
	|ПОМЕСТИТЬ ВТ_ОснованияТемп
	|ИЗ
	|	ВТ_Основания КАК ВТ_Основания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОбъектов КАК ВариантыОбъектов
	|		ПО (ВТ_Основания.ОбъектСистемы.ВестиВариантыОбъектов)
	|			И (НЕ ВариантыОбъектов.ПометкаУдаления)
	|			И ВТ_Основания.ОбъектСистемы = ВариантыОбъектов.Владелец
	|			И (ВТ_Основания.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|				ИЛИ ВТ_Основания.ВариантОбъекта = ВариантыОбъектов.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ОснованияТемп.Приоритет) КАК Приоритет,
	|	ВТ_ОснованияТемп.ОбъектСистемы КАК ОбъектСистемы,
	|	ВТ_ОснованияТемп.ВариантОбъекта КАК ВариантОбъекта
	|ПОМЕСТИТЬ ВТ_ОснованияМакс
	|ИЗ
	|	ВТ_ОснованияТемп КАК ВТ_ОснованияТемп
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОснованияТемп.ОбъектСистемы,
	|	ВТ_ОснованияТемп.ВариантОбъекта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Основания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОснованияТемп.ОбъектСистемы КАК ОбъектСистемы,
	|	ВТ_ОснованияТемп.ВариантОбъекта КАК ВариантОбъекта
	|ИЗ
	|	ВТ_ОснованияМакс КАК ВТ_ОснованияМакс
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОснованияТемп КАК ВТ_ОснованияТемп
	|		ПО ВТ_ОснованияМакс.Приоритет = ВТ_ОснованияТемп.Приоритет
	|			И ВТ_ОснованияМакс.ОбъектСистемы = ВТ_ОснованияТемп.ОбъектСистемы
	|			И ВТ_ОснованияМакс.ВариантОбъекта = ВТ_ОснованияТемп.ВариантОбъекта
	|ГДЕ
	|	НЕ ВТ_ОснованияТемп.ИсключитьВариантОбъекта
	|ИТОГИ
	|	МАКСИМУМ(ВариантОбъекта)
	|ПО
	|	ОбъектСистемы";
КонецФункции

Функция ПолучитьЗапросИсточникиОбъектаСистемы()
	Возврат
	
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОбъектыСистемыИсточники.ВариантОбъектаИсточника = ЗНАЧЕНИЕ(СПРАВОЧНИК.ВариантыОбъектов.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	ОбъектыСистемыИсточники.Ссылка КАК ОбъектСистемы,
	|	ОбъектыСистемыИсточники.ВариантОбъекта КАК ВариантОбъекта,
	|	ОбъектыСистемыИсточники.ИсключитьИсточник КАК ИсключитьИсточник,
	|	ОбъектыСистемыИсточники.ИсключитьВариантОбъекта КАК ИсключитьВариантОбъекта
	|ПОМЕСТИТЬ ВТ_ИсточникиТемп
	|ИЗ
	|	Справочник.ОбъектыСистемы.Источники КАК ОбъектыСистемыИсточники
	|ГДЕ
	|	ОбъектыСистемыИсточники.Источник = &ОбъектСистемы
	|	И (ОбъектыСистемыИсточники.ВариантОбъектаИсточника = &ВариантОбъекта
	|			ИЛИ ОбъектыСистемыИсточники.ВариантОбъектаИсточника = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ИсточникиТемп.Приоритет) КАК Приоритет,
	|	ВТ_ИсточникиТемп.ОбъектСистемы КАК ОбъектСистемы,
	|	ВТ_ИсточникиТемп.ВариантОбъекта КАК ВариантОбъекта
	|ПОМЕСТИТЬ ВТ_ИсточникиМакс
	|ИЗ
	|	ВТ_ИсточникиТемп КАК ВТ_ИсточникиТемп
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ИсточникиТемп.ОбъектСистемы,
	|	ВТ_ИсточникиТемп.ВариантОбъекта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИсточникиТемп.ОбъектСистемы КАК ОбъектСистемы,
	|	ВТ_ИсточникиТемп.ВариантОбъекта КАК ВариантОбъекта,
	|	ВТ_ИсточникиТемп.ИсключитьВариантОбъекта КАК ИсключитьВариантОбъекта
	|ПОМЕСТИТЬ ВТ_Источники
	|ИЗ
	|	ВТ_ИсточникиМакс КАК ВТ_ИсточникиМакс
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсточникиТемп КАК ВТ_ИсточникиТемп
	|		ПО ВТ_ИсточникиМакс.Приоритет = ВТ_ИсточникиТемп.Приоритет
	|			И ВТ_ИсточникиМакс.ОбъектСистемы = ВТ_ИсточникиТемп.ОбъектСистемы
	|			И ВТ_ИсточникиМакс.ВариантОбъекта = ВТ_ИсточникиТемп.ВариантОбъекта
	|ГДЕ
	|	НЕ ВТ_ИсточникиТемп.ИсключитьИсточник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИсточникиТемп
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ИсточникиМакс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_Источники.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	ВТ_Источники.ОбъектСистемы КАК ОбъектСистемы,
	|	ЕСТЬNULL(ВариантыОбъектов.Ссылка, ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)) КАК ВариантОбъекта,
	|	ВТ_Источники.ИсключитьВариантОбъекта КАК ИсключитьВариантОбъекта
	|ПОМЕСТИТЬ ВТ_ИсточникиТемп
	|ИЗ
	|	ВТ_Источники КАК ВТ_Источники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОбъектов КАК ВариантыОбъектов
	|		ПО (ВТ_Источники.ОбъектСистемы.ВестиВариантыОбъектов)
	|			И (НЕ ВариантыОбъектов.ПометкаУдаления)
	|			И ВТ_Источники.ОбъектСистемы = ВариантыОбъектов.Владелец
	|			И (ВТ_Источники.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|				ИЛИ ВТ_Источники.ВариантОбъекта = ВариантыОбъектов.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ИсточникиТемп.Приоритет) КАК Приоритет,
	|	ВТ_ИсточникиТемп.ОбъектСистемы КАК ОбъектСистемы,
	|	ВТ_ИсточникиТемп.ВариантОбъекта КАК ВариантОбъекта
	|ПОМЕСТИТЬ ВТ_ИсточникиМакс
	|ИЗ
	|	ВТ_ИсточникиТемп КАК ВТ_ИсточникиТемп
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ИсточникиТемп.ОбъектСистемы,
	|	ВТ_ИсточникиТемп.ВариантОбъекта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Источники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИсточникиТемп.ОбъектСистемы КАК ОбъектСистемы,
	|	ВТ_ИсточникиТемп.ВариантОбъекта КАК ВариантОбъекта
	|ИЗ
	|	ВТ_ИсточникиМакс КАК ВТ_ИсточникиМакс
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсточникиТемп КАК ВТ_ИсточникиТемп
	|		ПО ВТ_ИсточникиМакс.Приоритет = ВТ_ИсточникиТемп.Приоритет
	|			И ВТ_ИсточникиМакс.ОбъектСистемы = ВТ_ИсточникиТемп.ОбъектСистемы
	|			И ВТ_ИсточникиМакс.ВариантОбъекта = ВТ_ИсточникиТемп.ВариантОбъекта
	|ГДЕ
	|	НЕ ВТ_ИсточникиТемп.ИсключитьВариантОбъекта
	|ИТОГИ
	|	МАКСИМУМ(ВариантОбъекта)
	|ПО
	|	ОбъектСистемы";
КонецФункции

Функция ПолучитьЗапросПараметрыВыбора()
	Возврат
	
	"ВЫБРАТЬ
	|	КолонкиОбъектовДоступныеОбъектыСистемы.ОбъектСистемы КАК ОбъектСистемы,
	|	ЕСТЬNULL(ВариантыОбъектов.Ссылка, ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)) КАК ВариантОбъекта
	|ИЗ
	|	ПланВидовХарактеристик.КолонкиОбъектов.ДоступныеОбъектыСистемы КАК КолонкиОбъектовДоступныеОбъектыСистемы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОбъектов КАК ВариантыОбъектов
	|		ПО (КолонкиОбъектовДоступныеОбъектыСистемы.ОбъектСистемы.ВестиВариантыОбъектов)
	|			И КолонкиОбъектовДоступныеОбъектыСистемы.ОбъектСистемы = ВариантыОбъектов.Владелец
	|			И (КолонкиОбъектовДоступныеОбъектыСистемы.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|				ИЛИ КолонкиОбъектовДоступныеОбъектыСистемы.ВариантОбъекта = ВариантыОбъектов.Ссылка)
	|ГДЕ
	|	КолонкиОбъектовДоступныеОбъектыСистемы.Ссылка = &Колонка
	|
	|СГРУППИРОВАТЬ ПО
	|	КолонкиОбъектовДоступныеОбъектыСистемы.ОбъектСистемы,
	|	ЕСТЬNULL(ВариантыОбъектов.Ссылка, ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка))
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(КолонкиОбъектовДоступныеОбъектыСистемы.Исключить) = ЛОЖЬ";
КонецФункции

Функция ПолучитьЗапросПериодаДанных()
	Возврат
	
	"ВЫБРАТЬ
	|	ХранилищеЗначенийКолонокОбъектовСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ ВТ_Периоды
	|ИЗ
	|	РегистрСведений.ХранилищеЗначенийКолонокОбъектов.СрезПоследних(
	|			,
	|			Владелец = &Владелец
	|				И Источник = &Источник) КАК ХранилищеЗначенийКолонокОбъектовСрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АктивностьСтрокТаблицСрезПоследних.Период
	|ИЗ
	|	РегистрСведений.АктивностьСтрокТаблиц.СрезПоследних(
	|			,
	|			Владелец = &Владелец
	|				И Источник = &Источник) КАК АктивностьСтрокТаблицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ_Периоды.Период КАК Период
	|ИЗ
	|	ВТ_Периоды КАК ВТ_Периоды
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
КонецФункции

#КонецОбласти


