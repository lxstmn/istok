
Процедура ОбработкаПроведения(Отказ, РежимПроведения, ЭтотОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектСистемы", ЭтотОбъект.ОбъектСистемы);
	Запрос.УстановитьПараметр("ВариантОбъекта", ЭтотОбъект.ВариантОбъекта);
	Запрос.Текст = ПолучитьЗапросНастроекПроведения();
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Пока Результат.Следующий() Цикл
		Если Результат.Таблица.Пустая() Тогда
			ТаблицаДляПроведения = Новый Структура("Обход", 1);
		Иначе
			ТаблицаДляПроведения = ЭтотОбъект.ДополнительныеСвойства.ТаблицыОбъекта.Получить(Результат.Таблица);
		КонецЕсли;
		
		Если ТаблицаДляПроведения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтоДвиженияНезависимогоРегистра = Ложь;
		
		Если ЭтотОбъект.Движения.Найти(Результат.ИмяРегистра) = Неопределено Тогда
			ДвиженияРегистра = РегистрыСведений[Результат.ИмяРегистра].СоздатьНаборЗаписей();
			ДвиженияРегистра.Отбор.РазрезДанных.Установить(Результат.РазрезДанных);
			
			ЭтоДвиженияНезависимогоРегистра = Истина;
		Иначе
			ДвиженияРегистра = ЭтотОбъект.Движения[Результат.ИмяРегистра];
		КонецЕсли;
		
		ДвиженияРегистра.Записывать = Истина;
		
		РезультатНастройки = Результат.Выбрать();
		
		Для Каждого СтрокаТаблицаДляПроведения Из ТаблицаДляПроведения Цикл
			Движение = ДвиженияРегистра.Добавить();
			Движение.РазрезДанных = Результат.РазрезДанных;
			
			Пока РезультатНастройки.Следующий() Цикл
				Если ЗначениеЗаполнено(РезультатНастройки.КолонкаОбъекта) Тогда
					Если РезультатНастройки.КолонкаОбъекта.Предопределенный
						И РезультатНастройки.КолонкаОбъекта <> ПланыВидовХарактеристик.КолонкиОбъектов.ВидДвижения Тогда
						СистемноеНаименование = РезультатНастройки.КолонкаОбъекта.СистемноеНаименование;
						
						Если ПустаяСтрока(СистемноеНаименование) Тогда
							СистемноеНаименование = РезультатНастройки.КолонкаОбъекта.ИмяПредопределенныхДанных;
						КонецЕсли;
						
						Значение = ЭтотОбъект[СистемноеНаименование];
					Иначе
						СтрокаТаблицаКолонок = ЭтотОбъект.ДополнительныеСвойства.ТаблицаКолонок.НайтиСтроки(Новый Структура("ОбъектСистемы, Колонка", ЭтотОбъект.ОбъектСистемы, РезультатНастройки.КолонкаОбъекта));
						
						Если СтрокаТаблицаКолонок.Количество() = 1 Тогда
							Значение = СтрокаТаблицаКолонок[0].Значение;
						Иначе
							Продолжить;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли ЗначениеЗаполнено(РезультатНастройки.КолонкаТаблицы) Тогда
					Если РезультатНастройки.КолонкаТаблицы = ПланыВидовХарактеристик.КолонкиОбъектов.ВидДвижения Тогда
						Значение = ВидДвиженияНакопления[?(СтрокаТаблицаДляПроведения[РезультатНастройки.КолонкаТаблицы.СистемноеНаименование], "Приход", "Расход")];
					Иначе
						Значение = СтрокаТаблицаДляПроведения[РезультатНастройки.КолонкаТаблицы.СистемноеНаименование];
					КонецЕсли;
				Иначе
					Продолжить; //х = 3 / 0; // хз че происходит, ошибка в настройке документа
				КонецЕсли;
				
				Если ЭтоДвиженияНезависимогоРегистра
					И РезультатНастройки.ВключитьВОтбор Тогда
					ДвиженияРегистра.Отбор[РезультатНастройки.ИмяКолонкиРегистра].Установить(Значение);
				КонецЕсли;
				
				Движение[РезультатНастройки.ИмяКолонкиРегистра] = Значение;
			КонецЦикла;
			
			РезультатНастройки.Сбросить();
		КонецЦикла;
		
		Если ЭтоДвиженияНезависимогоРегистра Тогда
			ДвиженияРегистра.Записать();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ВестиКорректировки(ОбъектСистемы, ВариантОбъекта) Экспорт
	ВестиКорректировки = Ложь;
	
	Если ОбъектСистемы.ВестиКорректировки Тогда
		Если ОбъектСистемы.ВестиВариантыОбъектов Тогда
			Если ВариантОбъекта.ВестиКорректировки Тогда
				ВестиКорректировки = Истина;
			КонецЕсли;
		Иначе
			ВестиКорректировки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВестиКорректировки;
КонецФункции

Функция ПолучитьЗапросНастроекПроведения()
	Возврат
	
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ОбъектыСистемыРазрезыДанных.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	ОбъектыСистемыРазрезыДанных.ВариантОбъекта КАК ВариантОбъекта,
	|	ОбъектыСистемыРазрезыДанных.РазрезДанных КАК РазрезДанных,
	|	ОбъектыСистемыРазрезыДанных.Исключить КАК Исключить,
	|	ОбъектыСистемыРазрезыДанных.Таблица КАК Таблица,
	|	ОбъектыСистемыРазрезыДанных.КонтрольОстатков КАК КонтрольОстатков
	|ПОМЕСТИТЬ ВТ_РазрезыДанныхТемп
	|ИЗ
	|	Справочник.ОбъектыСистемы.РазрезыДанных КАК ОбъектыСистемыРазрезыДанных
	|ГДЕ
	|	ОбъектыСистемыРазрезыДанных.Ссылка = &ОбъектСистемы
	|	И (ОбъектыСистемыРазрезыДанных.ВариантОбъекта = ЗНАЧЕНИЕ(Справочник.ВариантыОбъектов.ПустаяСсылка)
	|			ИЛИ ОбъектыСистемыРазрезыДанных.ВариантОбъекта = &ВариантОбъекта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РазрезыДанныхТемп.РазрезДанных КАК РазрезДанных,
	|	МАКСИМУМ(ВТ_РазрезыДанныхТемп.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ВТ_РазрезыДанныхМакс
	|ИЗ
	|	ВТ_РазрезыДанныхТемп КАК ВТ_РазрезыДанныхТемп
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РазрезыДанныхТемп.РазрезДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РазрезыДанныхТемп.ВариантОбъекта КАК ВариантОбъекта,
	|	ВТ_РазрезыДанныхТемп.РазрезДанных КАК РазрезДанных,
	|	ВТ_РазрезыДанныхТемп.Таблица КАК Таблица,
	|	ВТ_РазрезыДанныхТемп.КонтрольОстатков КАК КонтрольОстатков
	|ПОМЕСТИТЬ ВТ_РазрезыДанных
	|ИЗ
	|	ВТ_РазрезыДанныхМакс КАК ВТ_РазрезыДанныхМакс
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РазрезыДанныхТемп КАК ВТ_РазрезыДанныхТемп
	|		ПО ВТ_РазрезыДанныхМакс.Приоритет = ВТ_РазрезыДанныхТемп.Приоритет
	|			И ВТ_РазрезыДанныхМакс.РазрезДанных = ВТ_РазрезыДанныхТемп.РазрезДанных
	|ГДЕ
	|	НЕ ВТ_РазрезыДанныхТемп.Исключить
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РазрезыДанных.ВариантОбъекта КАК ВариантОбъекта,
	|	ВТ_РазрезыДанных.РазрезДанных КАК РазрезДанных,
	|	ВТ_РазрезыДанных.Таблица КАК Таблица,
	|	ВТ_РазрезыДанных.КонтрольОстатков КАК КонтрольОстатков,
	|	РазрезыДанныхНастройкиКолонокРегистра.ИмяКолонкиРегистра КАК ИмяКолонкиРегистра,
	|	РазрезыДанныхНастройкиКолонокРегистра.КолонкаРазрезаДанных КАК КолонкаРазрезаДанных,
	|	РазрезыДанныхНастройкиКолонокРегистра.ВключитьВОтбор КАК ВключитьВОтбор
	|ПОМЕСТИТЬ ВТ_РазрезыДанныхНастройки
	|ИЗ
	|	ВТ_РазрезыДанных КАК ВТ_РазрезыДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РазрезыДанных.КолонокиРазрезаДанных КАК РазрезыДанныхНастройкиКолонокРегистра
	|		ПО ВТ_РазрезыДанных.РазрезДанных = РазрезыДанныхНастройкиКолонокРегистра.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РазрезыДанныхНастройки.РазрезДанных.ИмяРегистра КАК ИмяРегистра,
	|	ВТ_РазрезыДанныхНастройки.РазрезДанных КАК РазрезДанных,
	|	ВТ_РазрезыДанныхНастройки.Таблица КАК Таблица,
	|	ВТ_РазрезыДанныхНастройки.КонтрольОстатков КАК КонтрольОстатков,
	|	ВТ_РазрезыДанныхНастройки.ИмяКолонкиРегистра КАК ИмяКолонкиРегистра,
	|	ВТ_РазрезыДанныхНастройки.ВключитьВОтбор КАК ВключитьВОтбор,
	|	ОбъектыСистемыКолонкиРазрезовДанных.КолонкаОбъекта КАК КолонкаОбъекта,
	|	ОбъектыСистемыКолонкиРазрезовДанных.КолонкаТаблицы КАК КолонкаТаблицы
	|ПОМЕСТИТЬ ВТ_Итог
	|ИЗ
	|	ВТ_РазрезыДанныхНастройки КАК ВТ_РазрезыДанныхНастройки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСистемы.КолонкиРазрезовДанных КАК ОбъектыСистемыКолонкиРазрезовДанных
	|		ПО (ОбъектыСистемыКолонкиРазрезовДанных.Ссылка = &ОбъектСистемы)
	|			И ВТ_РазрезыДанныхНастройки.ВариантОбъекта = ОбъектыСистемыКолонкиРазрезовДанных.ВариантОбъекта
	|			И ВТ_РазрезыДанныхНастройки.РазрезДанных = ОбъектыСистемыКолонкиРазрезовДанных.РазрезДанных
	|			И ВТ_РазрезыДанныхНастройки.КолонкаРазрезаДанных = ОбъектыСистемыКолонкиРазрезовДанных.КолонкаРазрезаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АВТОНОМЕРЗАПИСИ() КАК Группировка,
	|	ВТ_Итог.ИмяРегистра КАК ИмяРегистра,
	|	ВТ_Итог.РазрезДанных КАК РазрезДанных,
	|	ВТ_Итог.Таблица КАК Таблица,
	|	ВТ_Итог.КонтрольОстатков КАК КонтрольОстатков
	|ПОМЕСТИТЬ ВТ_ИтогГруппировка
	|ИЗ
	|	ВТ_Итог КАК ВТ_Итог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИтогГруппировка.Группировка КАК Группировка,
	|	ВТ_Итог.ИмяРегистра КАК ИмяРегистра,
	|	ВТ_Итог.РазрезДанных КАК РазрезДанных,
	//|	ВТ_Итог.РазрезДанных.НеЗаполнятьРазрезДанных КАК НеЗаполнятьРазрезДанных,
	|	ВТ_Итог.Таблица КАК Таблица,
	|	ВТ_Итог.КонтрольОстатков КАК КонтрольОстатков,
	|	ВТ_Итог.ИмяКолонкиРегистра КАК ИмяКолонкиРегистра,
	|	ВТ_Итог.КолонкаОбъекта КАК КолонкаОбъекта,
	|	ВТ_Итог.КолонкаТаблицы КАК КолонкаТаблицы,
	|	ВТ_Итог.ВключитьВОтбор КАК ВключитьВОтбор
	|ИЗ
	|	ВТ_ИтогГруппировка КАК ВТ_ИтогГруппировка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Итог КАК ВТ_Итог
	|		ПО ВТ_ИтогГруппировка.ИмяРегистра = ВТ_Итог.ИмяРегистра
	|			И ВТ_ИтогГруппировка.РазрезДанных = ВТ_Итог.РазрезДанных
	|			И ВТ_ИтогГруппировка.Таблица = ВТ_Итог.Таблица
	|			И ВТ_ИтогГруппировка.КонтрольОстатков = ВТ_Итог.КонтрольОстатков
	|ИТОГИ
	|	МАКСИМУМ(ИмяРегистра),
	|	МАКСИМУМ(РазрезДанных),
	//|	МАКСИМУМ(НеЗаполнятьРазрезДанных),
	|	МАКСИМУМ(Таблица),
	|	МАКСИМУМ(КонтрольОстатков)
	|ПО
	|	Группировка";
КонецФункции


