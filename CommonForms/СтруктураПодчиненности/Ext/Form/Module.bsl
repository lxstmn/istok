
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	АнализируемыйДокумент = Параметры.АнализируемыйДокумент;
	
	Если АнализируемыйДокумент.Пустая() Тогда
		Отказ = Истина;
	Иначе
		ВывестиСтруктуруПодчиненности();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Для Каждого СтрокаСтруктураПодчиненности Из СтруктураПодчиненности.ПолучитьЭлементы() Цикл
		Элементы.СтруктураПодчиненности.Развернуть(СтрокаСтруктураПодчиненности.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СтруктураПодчиненностиПередНачаломИзменения(Элемент, Отказ)
	ПоказатьЗначение(, Элементы.СтруктураПодчиненности.ТекущиеДанные.ЭлементОбъектаСистемы);
	
	Отказ = Истина;
КонецПроцедуры

&НаСервере
Процедура ВывестиСтруктуруПодчиненности()
	ДЗ = РеквизитФормыВЗначение("СтруктураПодчиненности", Тип("ДеревоЗначений"));
	
	КореньДерева = АнализируемыйДокумент;
	
	Пока Истина Цикл
		Если КореньДерева.Основание.Пустая() Тогда
			Прервать;
		Иначе
			КореньДерева = КореньДерева.Основание;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЭлементыОбъектовСистемы.Ссылка КАК ЭлементОбъектаСистемы,
	|	ЭлементыОбъектовСистемы.Основание КАК ОснованиеКорректировка,
	|	ЭлементыОбъектовСистемы.Корректировка КАК Корректировка,
	|	ЭлементыОбъектовСистемы.Версия КАК Версия,
	|	ВЫБОР
	|		КОГДА ЭлементыОбъектовСистемы.Проведен
	|			ТОГДА 0
	|		КОГДА ЭлементыОбъектовСистемы.ПометкаУдаления
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтатусДокумента
	|ИЗ
	|	Документ.ЭлементыОбъектовСистемы КАК ЭлементыОбъектовСистемы
	|ГДЕ
	|	ЭлементыОбъектовСистемы.Основание В(&ДокументыДляВывода)
	|	И ЭлементыОбъектовСистемы.Корректировка = ЗНАЧЕНИЕ(Документ.ЭлементыОбъектовСистемы.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЭлементыОбъектовСистемы.Ссылка,
	|	ЭлементыОбъектовСистемы.Корректировка,
	|	ЭлементыОбъектовСистемы.Корректировка,
	|	ЭлементыОбъектовСистемы.Версия,
	|	ВЫБОР
	|		КОГДА ЭлементыОбъектовСистемы.Проведен
	|			ТОГДА 0
	|		КОГДА ЭлементыОбъектовСистемы.ПометкаУдаления
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|ИЗ
	|	Документ.ЭлементыОбъектовСистемы КАК ЭлементыОбъектовСистемы
	|ГДЕ
	//|	ЭлементыОбъектовСистемы.Основание = ЗНАЧЕНИЕ(Документ.ЭлементыОбъектовСистемы.ПустаяСсылка)
	|	ЭлементыОбъектовСистемы.Корректировка В(&ДокументыДляВывода)";
	
	ДокументыДляВывода = Новый Массив;
	ДокументыДляВывода.Добавить(КореньДерева);
	
	НоваяСтрока = ДЗ.Строки.Добавить();
	НоваяСтрока.ЭлементОбъектаСистемы = КореньДерева;
	НоваяСтрока.СтатусДокумента = ?(КореньДерева.Проведен, 0, ?(КореньДерева.ПометкаУдаления, 2, 1));
	
	Пока Истина Цикл
		Запрос.УстановитьПараметр("ДокументыДляВывода", ДокументыДляВывода);
		Результат = Запрос.Выполнить();
		ДокументыДляВывода.Очистить();
		
		Если Результат.Пустой() Тогда
			Прервать;
		КонецЕсли;
		
		Результат = Результат.Выбрать();
		
		Пока Результат.Следующий() Цикл
			СтрокаРодитель = ДЗ.Строки.Найти(Результат.ОснованиеКорректировка, "ЭлементОбъектаСистемы", Истина);
			
			НоваяСтрока = СтрокаРодитель.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
			
			ДокументыДляВывода.Добавить(Результат.ЭлементОбъектаСистемы);
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДЗ, "СтруктураПодчиненности");
КонецПроцедуры