
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ПустаяСтрока(Объект.ЦветXML) Тогда
		Элементы.ЦветXML.ЦветФона = СериализаторXDTO.XMLЗначение(Тип("Цвет"), Объект.ЦветXML);
	КонецЕсли;
	
	ДанныеПоРегистру = ПолучитьИнформациюОРегистре(Объект.ИмяРегистра);
	
	Если ДанныеПоРегистру.РегистрОпределен Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеПоРегистру, "ТипРегистра, Периодичность, РежимЗаписи, ВидРегистра");
	Иначе
		ТипРегистра = "Регистр не определен";
	КонецЕсли;
	
	Элементы.Периодичность.Видимость = Не ПустаяСтрока(Периодичность);
	Элементы.РежимЗаписи.Видимость = Не ПустаяСтрока(РежимЗаписи);
	Элементы.ВидРегистра.Видимость = Не ПустаяСтрока(ВидРегистра);
	Элементы.КолонокиРазрезаДанныхВключитьВОтбор.Видимость = РежимЗаписи = "Независимый";
КонецПроцедуры

&НаКлиенте
Процедура ЦветXMLНажатие(Элемент)
	ДиалогВыбораЦвета = Новый ДиалогВыбораЦвета;
	ДиалогВыбораЦвета.Цвет = Элементы.ЦветXML.ЦветФона;
	Оповещение = Новый ОписаниеОповещения("ВыборЦветаЗавершение", ЭтотОбъект);
	ДиалогВыбораЦвета.Показать(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ВыборЦветаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Не Результат = Неопределено Тогда
		Элементы.ЦветXML.ЦветФона = Результат;
		Объект.ЦветXML = СериализаторXDTO.XMLСтрока(Результат);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИмяРегистраПриИзменении(Элемент)
	ДанныеПоРегистру = ПолучитьИнформациюОРегистре(Объект.ИмяРегистра);
	
	Периодичность = "";
	РежимЗаписи = "";
	ВидРегистра = "";
	
	Если ДанныеПоРегистру.РегистрОпределен Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеПоРегистру, "ТипРегистра, Периодичность, РежимЗаписи, ВидРегистра");
	Иначе
		ТипРегистра = "Регистр не определен";
	КонецЕсли;
	
	Элементы.Периодичность.Видимость = Не ПустаяСтрока(Периодичность);
	Элементы.РежимЗаписи.Видимость = Не ПустаяСтрока(РежимЗаписи);
	Элементы.ВидРегистра.Видимость = Не ПустаяСтрока(ВидРегистра);
	Элементы.КолонокиРазрезаДанныхВключитьВОтбор.Видимость = РежимЗаписи = "Независимый";
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИнформациюОРегистре(ИмяРегистра)
	ДанныеПоРегистру = Новый Структура("РегистрОпределен, ТипРегистра, Периодичность, РежимЗаписи, ВидРегистра", Ложь);
	
	Регистр = Метаданные.РегистрыНакопления.Найти(ИмяРегистра);
	
	Если Регистр <> Неопределено Тогда
		ДанныеПоРегистру.РегистрОпределен = Истина;
		ДанныеПоРегистру.ТипРегистра = "Регистр накопления";
		ДанныеПоРегистру.ВидРегистра = Строка(Регистр.ВидРегистра);
	КонецЕсли;
	
	Если Не ДанныеПоРегистру.РегистрОпределен Тогда
		Регистр = Метаданные.РегистрыСведений.Найти(ИмяРегистра);
		
		Если Регистр <> Неопределено Тогда
			ДанныеПоРегистру.РегистрОпределен = Истина;
			ДанныеПоРегистру.ТипРегистра = "Регистр сведений";
			ДанныеПоРегистру.Периодичность = Строка(Регистр.ПериодичностьРегистраСведений);
			ДанныеПоРегистру.РежимЗаписи = Строка(Регистр.РежимЗаписи);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеПоРегистру;
КонецФункции
